{
  Resource: (function(name, spec, apiVersion="v1") {
    kind: name,
    apiVersion: apiVersion
  } + spec),
  # todo add spec strategy recreate
  Deployment: (function(name, spec) {
    "apiVersion": "apps/v1",
    "kind": "Deployment",
    "metadata": {
      "name": name,
      "labels": {
        "app.kubernetes.io/name": name,
      }
    },
    "spec": {
      "selector": {
        "matchLabels": {
          "app.kubernetes.io/name": name,
        }
      },
      "template": {
        "metadata": {
          "labels": {
            "app.kubernetes.io/name": name,
          }
        },
        "spec": spec + {
          nodeSelector: {
            "cloud.google.com/gke-spot": "true",
            //"cloud.google.com/compute-class": "Performance",
            //"cloud.google.com/machine-family": "c3"
          },
          //terminationGracePeriodSeconds: 25
        }
      }
    }
  }),
  SleepDeployment: (function(name, spec) self.Deployment(name, spec + {
    terminationGracePeriodSeconds: 0
  })),
  Container: (function(name, container) container + {
    name: name,
    resources: {
      requests: {
        memory: "10Mi",
        cpu: "10m",
      },
      /*
      limits: {
        memory: "30Mi",
        cpu: "30m",
      }*/
    }
  }),
  SleepContainer: (function(name, container) self.Container(name, container) + {
    #command: [ "sleep" ],
    #args: [ "infinity" ],
    # these respond to sleep
    # not actually lol jk
    command: [ "tail" ],
    args: [ "-f", "/dev/null" ],
  }),
  Service: (function(name) {
    "apiVersion": "v1",
    "kind": "Service",
    metadata: { name: name },
    "spec": {
      "selector": {
        "app.kubernetes.io/name": name,
      },
      "ports": [
        {
          "port": 80,
          "targetPort": "http"
        }
      ]
    }
  }),
  NetworkPolicy: (function(name) self.Resource("NetworkPolicy", {
    metadata: { name: name },
    "spec": {
      "podSelector": {
        "matchLabels": {
          "app.kubernetes.io/name": name,
        }
      },
      "policyTypes": [
        "Ingress"
      ],
      "ingress": [
        {
          "from": [
            {
              "podSelector": {
                "matchLabels": {
                  "app.kubernetes.io/name": "caddy"
                }
              }
            }
          ]
        }
      ]
    }
  }, apiVersion="networking.k8s.io/v1")),
  Secret: (function(name, data) self.Resource("Secret", {
    metadata: { name: name },
    data: { [key]: std.base64(data[key]) for key in std.objectFields(data) },
    immutable: true
  })),
  List: (function(list) self.Resource("List", {
    items: list
  })),
  Namespace: (function(name) self.Resource("Namespace", {
      metadata: {
        name: name,
        labels: {
          name: name
        }
      }
  })),
  ConfigMap: (function(name, data) self.Resource('ConfigMap', {
    data: data,
    immutable: true,
    metadata: {
      name: name
    }
  })),
  PersistentVolumeClaim: (function(name, capacity) self.Resource('PersistentVolumeClaim', {
    metadata: {
      name: name,
    },
    spec: {
      storageClassName: 'standard-rwo',
      # only used if binding to an existing volume
      #volumeName: name,
      # todo this is a bug waiting to happen
      # if a pod is on the same node? find
      # if its on a diff node? wont be scheduled...
      accessModes: [ 'ReadWriteOnce' ],
      resources: {
        requests: {
          storage: capacity,
        }
      },
    },
  })),
  PodDisruptionBudget: (function(name) self.Resource('PodDisruptionBudget', {
    metadata: {
      name: name
    },
    spec: {
      minAvailable: 1,
      selector: {
        matchLabels: {
          'app.kubernetes.io/name': name
        }
      }
    }
  }, apiVersion='policy/v1')),
}
